<mvt:item name="html_profile"/>
<mvt:if expr="g.show EQ 'quickview'">
    <mvt:item name="prod_quickview" />
    <mvt:exit />
</mvt:if>
<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <base href="&mvt:global:basehref;">
    <mvt:if expr="NOT ISNULL l.settings:product:page_title">
        <title>&mvt:product:page_title;</title>
    <mvt:elseif expr="NOT ISNULL l.settings:page:title">
        <title>&mvt:page:title;</title>
    <mvt:else>
        <title>&mvt:store:name;: &mvt:product:name;</title>
    </mvt:if>
    <mvt:item name="head" param="css_list" />
    <mvt:item name="head" param="head_tag" />
    <mvt:item name="attributemachine" param="head" />
    <mvt:item name="product_display_imagemachine" param="head" />
    <mvt:assign name="g.socialURL" value="l.settings:product:link" />
    <mvt:assign name="g.socialText" value="'Check out the deal on ' $ l.settings:product:name $ ' at ' $ g.store:name" />
    <mvt:do file="g.Module_Library_DB" name="l.success" value="ImageType_Load_Code('main', l.settings:imagetype)" />
    <mvt:do name="l.success" file="g.Module_Library_DB" value="ProductImage_Load_Type(l.settings:product:id, 1, l.settings:imagetype)" />
    <mvt:do name="l.success" file="g.Module_Library_DB" value="Image_Load_ID(l.settings:imagetype:image_id, l.settings:imagedata)" />
    <mvt:assign name="g.socialImage" value="g.baseurl $ l.settings:imagedata:image" />
    <meta property="og:title" content="&mvte:product:name;">
    <meta property="og:type" content="product">
    <meta property="og:image" content="&mvt:global:socialImage;">
    <meta property="og:url" content="&mvt:global:socialURL;">
    <meta property="og:site_name" content="&mvte:global:store:name;">
    <meta property="og:description" content="&mvte:global:socialText;">
    <meta property="og:locale" content="en_US">
    <meta name="twitter:card" content="summary_large_image">
    <meta name="twitter:site" content="@TWITTER_ACCOUNT">
    <meta name="twitter:creator" content="@TWITTER_ACCOUNT">
    <meta name="twitter:url" content="&mvt:global:socialURL;">
    <meta name="twitter:title" content="&mvte:product:name;">
    <meta name="twitter:description" content="&mvte:global:socialText;">
    <meta name="twitter:image" content="&mvt:global:socialImage;">
    <meta name="twitter:image:alt" content="&mvte:product:name;">

</head>
<body id="js-PROD" class="o-site-wrapper t-page-prod t-page-&mvt:global:pageClass; alt_prod_&mvte:page:code;">
    <mvt:item name="hdft" param="global_header" />
    
    <section class="o-layout">
        <div class="o-layout__item">
            <mvt:item name="hdft" param="header" />
        </div>
    </section>
    
    <section class="o-layout">
        <div class="o-layout__item">
            <mvt:item name="prod_ctgy_hdft" param="prod_header" />
        </div>
    </section>
    
    <!-- Start: Google Rich Snippets -->
    <mvt:comment>This section strips any HTML from the product description.</mvt:comment>
    <mvt:assign name="g.allowed_tags" value="''" />
    <mvt:assign name="l.settings:product:stripped_descrip" value="miva_html_strip(l.settings:product:descrip, g.allowed_tags)"/>

    <div itemscope itemtype="http://schema.org/Product">
        <meta itemprop="name" content="&mvte:product:name;" />
        <meta itemprop="image" content="&mvt:global:socialImage;" />
        <meta itemprop="category" content="&mvte:category:name;" />
        <meta itemprop="description" content="&mvte:product:stripped_descrip;" />
        <mvt:if expr="NOT ISNULL l.settings:product:customfield_values:customfields:brand">
            <meta itemprop="brand" content="&mvte:product:customfield_values:customfields:brand;" />
        </mvt:if>
        <div itemprop="offers" itemscope itemtype="http://schema.org/Offer">
            <mvt:if expr="NOT ISNULL l.settings:product:sku">
                <meta itemprop="sku" content="&mvte:product:sku;" />
            <mvt:else>
                <meta itemprop="sku" content="&mvte:product:code;" />
            </mvt:if>
            <mvt:item name="product_display" />
            <mvt:item name="attributemachine" param="body" />
            <script>
                if (typeof am&mvt:product:id; !== 'undefined') {
                    var attrMachCall = am&mvt:product:id;,
                        attrPossible = amPossible&mvt:product:id;;
                };
            </script>
            <meta itemprop="priceCurrency" content="USD" />
            <meta itemprop="seller" content="&mvte:global:store:name;" />
            <meta itemprop="itemCondition" content="new" />
            <mvt:if expr="l.settings:product:inv_active">
                <meta itemprop="availability" content="&mvte:product:inv_short;" />
                <meta itemprop="inventoryLevel" content="&mvte:product:inv_available;" />
            </mvt:if>
        </div>
    </div>
    <!-- End: Google Rich Snippets -->
    
    <section class="o-layout">
        <div class="o-layout__item">
            <mvt:item name="prod_ctgy_hdft" param="prod_footer" />
        </div>
    </section>
    
    <section class="o-layout">
        <div class="o-layout__item">
            <mvt:item name="hdft" param="footer" />
        </div>
    </section>
    
    <mvt:item name="hdft" param="global_footer" />
    
    <mvt:if expr="g.apply_ng GT 0">
        
        <mvt:assign name="g.theme_path" value="'themes/shadows'" />
        <mvt:comment> Polyfills </mvt:comment>
        <script src="https://cdn.polyfill.io/v2/polyfill.min.js"></script>
        <mvt:comment> Include Angular Files </mvt:comment>
        <mvt:comment><script src="https://cdnjs.cloudflare.com/ajax/libs/angular.js/1.7.2/angular.min.js" integrity="sha256-ruP2+uorUblSeg7Tozk75u8TaSUKRCZVvNV0zRGxkRQ=" crossorigin="anonymous" defer></script></mvt:comment>
        <mvt:comment><script src="https://cdnjs.cloudflare.com/ajax/libs/angular.js/1.7.2/angular-sanitize.min.js" integrity="sha256-FnMl43xSx3jHmcs7t2LJ3htfsCeo99XORPvzOVQN/tw=" crossorigin="anonymous" defer></script></mvt:comment>

        <mvt:comment>removed since angular is already being loaded</mvt:comment>
        <mvt:comment><script src="&mvte:global:theme_path;/extensions/product-layout-ng/js/angular.min.js" type="text/javascript" defer></script></mvt:comment>
        <mvt:comment><script src="&mvte:global:theme_path;/extensions/product-layout-ng/js/angular-sanitize.min.js" type="text/javascript" defer></script></mvt:comment>

        <mvt:comment> APP JS </mvt:comment>
        

        <mvt:comment>  
            <!-- temp off -->    
            <mvt:assign name="g.ngappfile" value="'/extensions/product-layout-ng/js/app.js'" />
            <mvt:assign name="l.settings:mainscriptsjs:path" value="'/mm5/' $ g.theme_path $ g.ngappfile" />
            <mvt:assign name="l.settings:mainscriptsjs:exists" value="sexists( l.settings:mainscriptsjs:path )" />
            <mvt:assign name="l.settings:mainscriptsjs:time" value="stime( l.settings:mainscriptsjs:path )" />
        </mvt:comment>

        
        <mvt:comment>
            - LAZY LOAD OUR APP
            ++ Because searchspring may also load on this page we need to check if we need to load angular or use SS angular
        </mvt:comment>
        <mvt:assign name="g.ngappfile" value="'/extensions/product-layout-ng/js/load.js'" />
        <mvt:assign name="l.settings:mainscriptsjs:path" value="'/mm5/' $ g.theme_path $ g.ngappfile" />
        <mvt:assign name="l.settings:mainscriptsjs:exists" value="sexists( l.settings:mainscriptsjs:path )" />
        <mvt:assign name="l.settings:mainscriptsjs:time" value="stime( l.settings:mainscriptsjs:path )" />
        <script type="text/javascript" src="&mvte:global:theme_path;&mvte:global:ngappfile;?v=&mvt:mainscriptsjs:time;" defer></script>


    </mvt:if>
    <mvt:comment><script type="text/javascript" src="&mvte:global:theme_path;&mvte:global:ngappfile;?v=&mvt:mainscriptsjs:time;" defer></script></mvt:comment>
    <mvt:comment><script src="&mvte:global:theme_path;/extensions/product-layout-ng/js/factories/factories.js?v=&mvt:mainscriptsjs:time;" type="text/javascript" defer></script></mvt:comment>
    <mvt:comment> APP CSS</mvt:comment>
    <mvt:assign name="g.ngappfile" value="'/extensions/product-layout-ng/css/app.css'" />
    <mvt:assign name="l.settings:mainscriptsjs:path" value="'/mm5/' $ g.theme_path $ g.ngappfile" />
    <mvt:assign name="l.settings:mainscriptsjs:exists" value="sexists( l.settings:mainscriptsjs:path )" />
    <mvt:assign name="l.settings:mainscriptsjs:time" value="stime( l.settings:mainscriptsjs:path )" />
    <link rel="stylesheet" href="&mvte:global:theme_path;&mvte:global:ngappfile;?v=&mvt:mainscriptsjs:time;">
</body>
</html>